import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'java-library'

    id 'idea'
    id 'eclipse'

    // Publishing
    id 'com.vanniktech.maven.publish' version '0.34.0'

    // Utility
    id 'jacoco'
    id 'org.sonarqube' version '6.3.1.5724'
    id 'com.diffplug.spotless' version '7.0.4'
}

// Project properties
group = "$GROUP"
version = "$VERSION_NAME"
description = "$POM_DESCRIPTION"

// Source code properties
java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    compileJava.options.encoding = 'UTF-8'
}

// Enable Spotless code formatting rules
spotless {
    java {
        googleJavaFormat()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('sourceJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

// Configure several tasks additionally for Gradle
tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = StandardCharsets.UTF_8.name()
    dependsOn(spotlessJavaCheck)
}

test {
    useJUnitPlatform()

    // Report is always generated after tests run
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // Tests are required to run before generating the report
    dependsOn test

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

sonar {
    properties {
        property "sonar.projectKey", "SuppieRK_java-throwable-utils"
        property "sonar.organization", "suppierk"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}